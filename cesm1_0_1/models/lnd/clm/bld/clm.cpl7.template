#! /bin/csh -f

if !(-d $CASEBUILD) mkdir $CASEBUILD
if !(-d $CASEBUILD/clmconf) mkdir $CASEBUILD/clmconf

#--------------------------------------------------------------------
# Invoke configure
#--------------------------------------------------------------------

set cfgdir = $CASEBUILD/clmconf  
set config = $cfgdir/config_cache.xml
cd $cfgdir

# Global River Transport model off for pt1 mode
if ($LND_GRID == pt1 || $PTS_MODE == TRUE ) then
   set rtm = "off"
else
   set rtm = "on"
endif

setenv COMP "unknown"
if ($COMP_INTERFACE == 'MCT' ) setenv COMP mct
if ($COMP_INTERFACE == 'ESMF') setenv COMP esmf

set config_opts = "-spmd -mode ext_ccsm_seq $CLM_CONFIG_OPTS -rtm $rtm -glc_nec $GLC_NEC"
$CODEROOT/lnd/clm*/bld/configure -comp_intf cpl_\$COMP -clm_root \$CODEROOT/lnd/clm -usr_src \$CASEROOT/SourceMods/src.clm $config_opts || exit -1 

#--------------------------------------------------------------------
# Invoke build-namelist
#--------------------------------------------------------------------

# --- set hybrid run initial dataset 
set finidat = " "
if ($RUN_TYPE == hybrid) set finidat = " finidat = '${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc' "

# --- set branch run restart dataset 
set nrevsn = " "
if ($RUN_TYPE == branch) set nrevsn   = " nrevsn = '${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc' "

@ clm_dtime  = ( 3600 * 24 ) / $LND_NCPL

if ( $rtm == "on" )then
  @ rtm_nsteps = ( 3600 * 3  ) / $clm_dtime  # rtm is done every 3 hours
  set rtmnsteps = " rtm_nsteps       =  $rtm_nsteps"
else
  set rtmnsteps = " "
endif

set co2ppmv = " co2_ppmv         =  $CCSM_CO2_PPMV"
set co2type = " co2_type         =  '$CLM_CO2_TYPE'"

cat >> $cfgdir/ccsm_namelist << EOF1
 &clm_inparm
 dtime            =  $clm_dtime
$rtmnsteps
$co2ppmv
$co2type
$finidat
$nrevsn
 hist_mfilt       =  1
 hist_nhtfrq      =  0
/
EOF1

# Allow the user to supply namelist values by putting them in the file
# $CASEROOT/user_nl_clm.  The file should contain
# a valid namelist, just like any file that supplies user input to
# build-namelist via the -infile argument.
if (-f $CASEROOT/user_nl_clm) then
   cat $CASEROOT/user_nl_clm >> $cfgdir/ccsm_namelist || exit -1
endif

#
# Other options to build-namelist
#
if ($RUN_TYPE == startup ) then
   set START_TYPE = "default"
   if ($CLM_FORCE_COLDSTART == on) set START_TYPE = "cold"
else
   set START_TYPE = $RUN_TYPE
   if ($RUN_TYPE == hybrid ) set START_TYPE = "startup"
endif

set mask = " "
if ($ATM_GRID != $OCN_GRID) then
  set mask = "-mask $OCN_GRID"
endif

if ($LND_GRID == pt1 ) then
   set RESOLUTION = $CLM_PT1_NAME
   if ( $RESOLUTION == "" ) set RESOLUTION = "default"
else
   set RESOLUTION = $LND_GRID
endif

# following logic may need tweaking to account for configurations where start year is important
if ($RUN_STARTDATE =~ *-01-01* || $RUN_STARTDATE =~ *-09-01*) then
    set ignore = "-ignore_ic_year"
else
    set ignore = "-ignore_ic_date"
endif

if ("$CLM_NML_USE_CASE" == "UNSET") then
    set usecase = " "
else
    set usecase = "-use_case $CLM_NML_USE_CASE"
endif

set options    = ""
if ( "$CLM_USRDAT_NAME" != "UNSET" )then
   set options    = "-clm_usr_name $CLM_USRDAT_NAME"
   if ( "$RESOLUTION" == "" || "$RESOLUTION" == "UNSET" ) set RESOLUTION = "$CLM_USRDAT_NAME"
endif
set options = "$options -config $CASEBUILD/clmconf/config_cache.xml -res $RESOLUTION $mask $ignore $usecase "
set options = "$options -infile ccsm_namelist -clm_start_type $START_TYPE $CLM_BLDNML_OPTS"
if ("$COMP_ATM" == "datm" )then
   set options = "$options  -datm_presaero $DATM_PRESAERO"
else
   set options = "$options  -datm_presaero prognostic"
endif
if ("$COMP_GLC" != "sglc" )then
    set options = "$options -glc_grid $GLC_GRID"
endif
$CODEROOT/lnd/clm*/bld/build-namelist $options -namelist "&clm_inparm $CLM_NAMELIST_OPTS /" -csmdata \$DIN_LOC_ROOT \-inputdata $CASEBUILD/clm.input_data_list || exit -1

# ---------------------------------------------------------------------------
#  Create namelist and prestage data script
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildnml.csh << EOF1
#! /bin/csh -f 

#******************************************************************#
#                  WARNING:                                        #
# - CAM and CLM namelist variable dtime must have same values      #
# - If the user changes any input datasets - be sure to give it a  #
#   unique filename. Do not duplicate any existing input files     #
#******************************************************************#

set exedir = \$RUNDIR; cd \$exedir

cat >! lnd_in << EOF
EOF1
cat lnd_in >> $CASEBUILD/clm.buildnml.csh || exit -1
cat >> $CASEBUILD/clm.buildnml.csh << EOF1

EOF
EOF1

#
# Remove temporary namelist in the clmconf directory
# (Leave config_cache.xml file as it documents what was done here)
#
\rm -f $CASEBUILD/clmconf/lnd_in $CASEBUILD/clmconf/ccsm_namelist

#--------------------------------------------------------------------
# Create clm.buildexe.csh
#--------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildexe.csh << EOF2
#! /bin/csh -f 

set objdir = \$OBJROOT/lnd/obj; cd \$objdir

#--------------------------------------------------------------------
# check basic task and thread settings
#--------------------------------------------------------------------

cp -f \$CASEBUILD/clmconf/misc.h       .tmp
cmp -s .tmp misc.h       || mv -f .tmp misc.h
cp -f \$CASEBUILD/clmconf/preproc.h    .tmp
cmp -s .tmp preproc.h    || mv -f .tmp preproc.h
cp -f \$CASEBUILD/clmconf/CCSM_cppdefs .tmp
cmp -s .tmp CCSM_cppdefs || mv -f .tmp CCSM_cppdefs

setenv COMP "unknown"
if (\$COMP_INTERFACE == 'MCT' ) setenv COMP mct
if (\$COMP_INTERFACE == 'ESMF') setenv COMP esmf

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath
EOF2
cat $CASEBUILD/clmconf/Filepath >> $CASEBUILD/clm.buildexe.csh
cat >> $CASEBUILD/clm.buildexe.csh << EOF2
EOF

#
# Build the clm library
#
set clmdefs = "`cat \$CASEBUILD/clmconf/CCSM_cppdefs`"
gmake complib -j \$GMAKE_J MODEL=clm COMPLIB=\$LIBROOT/liblnd.a MACFILE=\$CASEROOT/Macros.\$MACH USER_CPPDEFS="\$clmdefs" -f \$CASETOOLS/Makefile || exit 2

EOF2

#
# Remove any temporary configure files in the clmconf directory
# (Some files are still used in clm.buildexe.csh, so leave those around: CCSM_cppdefs and *.h)
#
\rm -f $CASEBUILD/clmconf/Filepath
